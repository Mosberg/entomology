plugins {
    id "fabric-loom" version "1.14-SNAPSHOT"
    id "maven-publish"
    id "checkstyle"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // Testing dependencies
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.1"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

checkstyle {
    toolVersion = "10.12.5"
    configFile = file("${rootDir}/checkstyle.xml")
}

loom {
    splitEnvironmentSourceSets()

    mods {
        create("entomology") {
            sourceSet(sourceSets.main)
            sourceSet(sourceSets.client)
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(java_version as int)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = java_version as int
}

tasks.processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}
